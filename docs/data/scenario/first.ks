*start

[title name="戦士対怪物"]
[hidemenubutton]
[wait time=100]
[freeimage layer="base"]

[cm]

「戦士対怪物」[l][r]

囲まれた！　一対三だ！[l][r]

接敵する前に、冷気の魔法で一体倒そう！[r]
冷気の魔法を……[l][r]

[link target=*ice_slime] →スライムに唱える。 [endlink][r]
[link target=*ice_skelton] →スケルトンに唱える。 [endlink][r]
[link target=*ice_troll] →トロルに唱える。 [endlink][r]
[s]

*ice_slime

[cm]

火球を叩きつけられたスライムはみるみるうちに蒸発していく！[l][r]

しかし、両側からスケルトンとトロルが迫る！[r]
素早いが骨だけのスケルトンと鈍重だが頑強なトロル。[r]
こちらが先制攻撃するのは……[l][r]

[link target=*first_skelton] →スケルトンだ！ [endlink][r]
[link target=*first_troll] →トロルだ！ [endlink][r]
[s]

*ice_skelton

[cm]

スケルトンの骨には結露した氷柱ができただけだ！[l][r]

三体に同時攻撃され、死んだ！[l][r]

[jump target=*hint_cold]

*ice_troll

[cm]

トロルは両腕で冷気を防いだ！[r]
致命傷には至らなかった！[l][r]

三体に同時攻撃され、死んだ！[l][r]

[jump target=*hint_cold]

*hint_cold

[cm]

おお、勇者よ！　死んでしまうとは情けない！[l][r]

スライムは属性攻撃に弱いぞ！[r]
アンデッドのスケルトンに冷気は効かないぞ！[r]
トロルは強敵だ！　まずは雑魚から片付けろ！[l][r]

［～敗北～］[l][r]

[jump target=*start]

*first_skelton

[cm]

魔法の剣は容易にスケルトンの脊椎を両断した！[r]
スケルトンは地面に崩れ落ちる。[l][r]

一対一となったが、トロルはそれも気にせず突っ込んできた！[l][r]

「なかなかやるようだな！　勇者！」[l][r]

「おまえ、トロルなのに流暢に喋れるのかよ！」[l][r]

「ら抜き言葉とはお里が知れるぞ！」[l][r]

「うるせえええええ！！！　必殺・スター・バースト・メテオ・ストライク・インパクト・零式・改・超・オリジナル！！！！」[l][r]

「ぐわああああああああああ！！！！！」[l][r]

［～勝利～］[l][r]

[jump target=*start]

*first_troll

[cm]

魔法の剣はトロルの胸を貫通し、胸に突き刺さった！[r]
しかし、トロルの再生力により、剣は抜けなくなった！[r]

背を向けている間に近づいていたスケルトンの骨ばった手が背中に突き刺さって心臓を止めた！[l][r]

[jump target=*hint_first]

*hint_first

[cm]

おお、勇者よ！　死んでしまうとは情けない！[l][r]

スケルトンはトロルより速くこちらに近づいてくるぞ！[r]
トロルは強敵だ！　まずは雑魚から片付けろ！[l][r]

［～敗北～］[l][r]

[jump target=*start]
